Program.Sub.ScreenSU.Start
Gui.Form..Create
Gui.Form..Caption("Product Line Maintenance")
Gui.Form..Size(5880,9105)
Gui.Form..MinX(0)
Gui.Form..MinY(0)
Gui.Form..Position(0,0)
Gui.Form..BackColor(-2147483633)
Gui.Form..MousePointer(0)
Gui.Form..Event(UnLoad,Form_UnLoad)
Gui.Form.lbl1.Create(Label,"PL",True,420,255,0,210,375,True,0,"Arial",8,-2147483633,0)
Gui.Form.txtPL.Create(TextBox,"",True,435,300,0,210,615,True,0,"Arial",8,-2147483643,1)
Gui.Form.txtPL.ControlGroup(1)
Gui.Form.txtPL.Locked(True)
Gui.Form.txtDesc.Create(TextBox,"",True,2400,300,0,870,615,True,0,"Arial",8,-2147483643,1)
Gui.Form.txtDesc.ControlGroup(1)
Gui.Form.txtDesc.Locked(True)
Gui.Form.cmdbrowser.Create(Button)
Gui.Form.cmdbrowser.Size(330,330)
Gui.Form.cmdbrowser.Position(3480,600)
Gui.Form.cmdbrowser.Caption("^")
Gui.Form.cmdbrowser.Event(Click,cmdbrowser_Click)
Gui.Form.cmdAdd.Create(Button)
Gui.Form.cmdAdd.Size(855,375)
Gui.Form.cmdAdd.Position(4110,570)
Gui.Form.cmdAdd.Caption("Add")
Gui.Form.cmdAdd.Event(Click,cmdAdd_Click)
Gui.Form.GsGCPL.Create(GsGridControl)
Gui.Form.GsGCPL.Size(5160,7050)
Gui.Form.GsGCPL.Position(210,1110)
Gui.Form.lbl2.Create(Label,"Description",True,1935,255,0,870,375,True,0,"Arial",8,-2147483633,0)
Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start
Program.Sub.Preflight.End

Program.Sub.Main.Start
'Created by SKC on 5/7/2018 for Cap & Associates.
'This is the maintenance program that will store the PL that need to be removed from the Consolidated Parts report.
'The custom table is GCG_5025_PL_Maint, the report is GCG_5025_BM_Consol_Parts.rpt.  These are the only parts of the program since the parts are removed through crystal linking to the custom table and suppressing the lines.
F.Intrinsic.Control.Try
V.Local.sError.Declare
v.Local.sSql.Declare

f.ODBC.Connection!con.OpenCompanyConnection(500)
'get the Product lines from the custom table and set up grid
F.Intrinsic.Control.CallSub(DTGrid)
gui.Form..Show

F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	F.Intrinsic.Control.CallSub(Form_Unload)
F.Intrinsic.Control.EndTry
Program.Sub.Main.End

Program.Sub.Form_UnLoad.Start
F.Intrinsic.Control.Try
V.Local.sError.Declare
F.ODBC.Connection!con.Close
F.Intrinsic.Control.End
F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	F.Intrinsic.Control.End
F.Intrinsic.Control.EndTry
Program.Sub.Form_UnLoad.End

Program.Sub.cmdbrowser_Click.Start
F.Intrinsic.Control.Try
V.Local.sError.Declare
v.Local.sRet.Declare
'Product Line Browser will populate textboxes when selection is made
F.Intrinsic.UI.Browser(130,"", v.Local.sRet)
F.Intrinsic.Control.If(v.Local.sRet, !=, v.Ambient.Cancel)
	F.Intrinsic.String.Split(v.Local.sRet, "*!*", v.Local.sRet)
	gui.Form.txtPL.Text(v.Local.sRet(0).trim)
	gui.Form.txtDesc.Text(v.Local.sRet(1).trim)
F.Intrinsic.Control.EndIf
F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	F.Intrinsic.Control.CallSub(Form_Unload)
F.Intrinsic.Control.EndTry
Program.Sub.cmdbrowser_Click.End

Program.Sub.cmdAdd_Click.Start
F.Intrinsic.Control.Try
V.Local.sError.Declare
v.Local.iID.Declare
V.Local.sSql.Declare
'Make sure a product line was selected and add to grid, then save to DB.  
F.Intrinsic.Control.If(v.Screen.Form!txtPL.text, !=, "")
	v.Local.sSql.Set("Select Max(ID) as ID from GCG_5025_PL_Maint")
	F.ODBC.Connection!con.ExecuteAndReturn(v.Local.sSql, v.Local.iID)
	F.Data.DataTable.AddRow("dtPL", "ID", v.Local.iID.++, "ProductLine", v.Screen.Form!txtPL.Text, "PL_Desc", v.Screen.Form!txtDesc.Text)
	Gui.Form.txtDesc.Text("")
	gui.Form.txtPL.Text("")
	F.Data.DataTable.SaveToDB("dtPL", "con", "GCG_5025_PL_Maint","ID",7)
	F.Data.DataTable.AcceptChanges("dtPL")
F.Intrinsic.Control.EndIf
F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	F.Intrinsic.Control.CallSub(Form_Unload)
F.Intrinsic.Control.EndTry
Program.Sub.cmdAdd_Click.End

Program.Sub.DTGrid.Start
F.Intrinsic.Control.Try
V.Local.sError.Declare
F.Data.DataTable.CreateFromSQL("dtPL", "con", "Select ID,ProductLine, PL_Desc from GCG_5025_PL_Maint Order by ID", true)
Gui.Form.GsGCPL.AddGridviewFromDatatable("dvPL", "dtPL")
Gui.Form.GsGCPL.MainView("dvPL")
gui.Form.GsGCPL.SetGridviewProperty("dvPL", "AllowSort", True)
Gui.Form.GsGCPL.SetGridviewProperty("dvPL", "AllowFilter", True)
gui.Form.GsGCPL.SetGridviewProperty("dvPL", "Editable", True)
gui.Form.GsGCPL.SetGridviewProperty("dvPL", "ReadOnly", False)
gui.Form.GsGCPL.SetColumnProperty("dvPL", "PL_Desc", "AllowEdit", False)
gui.Form.GsGCPL.SetColumnProperty("dvPL", "ID", "Visible", False)
gui.Form.GsGCPL.SetColumnProperty("dvPL", "ProductLine", "AllowEdit", False)
gui.Form.GsGCPL.SetColumnProperty("dvPL", "PL_Desc", "Caption", "Description")
gui.Form.GsGCPL.SetGridviewProperty("dvPL", "AllowDeleteRows", True)
F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	F.Intrinsic.Control.CallSub(Form_Unload)
F.Intrinsic.Control.EndTry
Program.Sub.DTGrid.End
